plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.5.11"
    id("xyz.jpenilla.run-paper") version "2.2.3"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.rexi'
version = '0.5-BETA'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "placeholderapi"
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    paperweight.paperDevBundle "1.20.4-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-api:4.17.0"
    compileOnly 'net.luckperms:api:5.4'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.33'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.46.1.0'
    compileOnly "me.clip:placeholderapi:2.11.5"
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}
shadowJar {
    archiveClassifier.set('all')
    configurations = [project.configurations.runtimeClasspath]
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
